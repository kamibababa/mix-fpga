* Count the number of coin combinations                                         
*                                                                               
TERM       EQU  19                                                              
VAL        EQU  4:5                                                             
AMT        EQU  1:3                                                             
*                                                                               
* Start of the program.                                                         
           ORIG 1000                                                            
PROG       OUT  PRMPT(TERM)                                                     
* Get the value we want combos for                                              
           IN   INPT1(TERM)                                                     
           JBUS *(TERM) 
           LDA  INPT1                                                           
           LDX  INPT2                                                           
           JAP  1F                                                              
           JXZ  PROG                                                            
1H         CMPX PRMPT(5:5)                                                      
           JNE  3F                                                              
           SRAX 1                                                               
           JMP  1B                                                              
3H         NUM                                                                  
           CMPA MAX                                                             
           JG   PROG                                                            
           STZ  MAX                                                             
           STA  WANTED                                                          
*                                                                               
* Get a current value of the coins we have                                      
CURRVAL    LD6  TYPES                                                           
           LDA  COINS,6(AMT)                                                    
2H         DEC6 1                                                               
           ADD  COINS,6(AMT)                                                    
           J6NZ 2B                                                              
*                                                                               
* If less than goal, add a coin                                                 
           CMPA WANTED                                                          
           JGE  1F                                                              
           LDA  COINS(AMT)                                                      
           ADD  COINS(VAL)                                                      
           STA  COINS(AMT)                                                      
           JMP  CURRVAL                                                         
1H         JG   3F                                                              
*                                                                               
* It was equal, print, and the fall through                                     
           LDX  MAX                                                             
           INCX 1                                                               
           LD6  TYPES                                                           
           ENT5 0                                                               
           STX  MAX                                                             
           LDA  MAX                                                             
           CHAR                                                                 
           STX  PBUF,5                                                          
1H         INC5 2                                                               
           ENTA 0                                                               
           LDX  COINS,6(AMT)                                                    
           DIV  COINS,6(VAL)                                                    
6H         CHAR                                                                 
           STX  PBUF,5(3:5)                                                     
           DEC6 1                                                               
           J6NN 1B                                                                         
           OUT  PBUF(TERM)     
           JBUS *(TERM) 
*                                                                               
* Equal or Greater, find first non-zero spot, add one to the next               
* spot up, and then zero it out.                                                
3H         ENT5 0                                                               
5H         LDA  COINS,5(AMT)                                                    
           INC5 1                                                               
           CMP5 TYPES                                                           
           JG   DONE                                                            
           JAZ  5B                                                              
           LDA  COINS,5(AMT)                                                    
           ADD  COINS,5(VAL)                                                    
           STA  COINS,5(AMT)                                                    
           DEC5 1                                                               
           STZ  COINS,5(AMT)                                                    
           JMP  CURRVAL                                                         
DONE       HLT                                                                  
* Tables to keep the values and counts                                          
* The below can be used to test for 1000 (1:2), 100 (3:4), and 10 (5:5)         
MAX        CON  499                                                             
WANTED     CON  62                                                              
TYPES      CON  5                                                               
COINS      CON  1                                                               
           CON  5                                                               
           CON  10                                                              
           CON  25                                                              
           CON  50                                                              
           CON  100                                                             
PRMPT      ALF  WHAT                                                            
           ALF  AMOUN                                                           
           ALF  T <IN                                                           
           ALF   CENT                                                           
           ALF  S> DO                                                           
           ALF   YOU                                                            
           ALF  WANT                                                            
           ALF  MAX 2                                                           
           ALF  20:                                                             
           ORIG PRMPT+14                                                        
INPT1      CON  0                                                               
INPT2      CON  0                                                               
* This will be our printline                                                    
           ORIG PRMPT+28                                                        
PBUF       CON  0                                                               
           ALF    DLR                                                           
           ALF  :                                                               
           ALF    HLF                                                           
           ALF  :                                                               
           ALF    QTR                                                           
           ALF  :                                                               
           ALF    DMS                                                           
           ALF  :                                                               
           ALF   NCKL                                                           
           ALF  :                                                               
           ALF    PNY                                                           
           ALF  :                                                               
           END  PROG                                                            
