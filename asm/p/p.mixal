* EXAMPLE PROGRAM ... TABLE OF PRIMES
*
L	EQU	500	The number of primes to find
TERM	EQU	19	Unit number of the line printer
PRIME	EQU	2999
BUF0	EQU	2000	Memory area for BUFFER[0]
BUF1	EQU	BUF0+25	Memory area for BUFFER[1]
	ORIG	1000
START	IOC	0(TERM)	Skip to new page
	LD1	=1-L=
	LD2	=3=
2H	INC1	1
	ST2	PRIME+L,1
	J1Z	2F
4H	INC2	2
	ENT3	2
6H	ENTA	0
	ENTX	0,2
	DIV	PRIME,3
	JXZ	4B
	CMPA	PRIME,3
	INC3	1
	JG	6B
	JMP	2B
2H	OUT	TITLE(TERM)
	ENT4	BUF1+10
	ENT5	-50
2H	INC5	L+1
4H	LDA	PRIME,5
	CHAR
	STX	0,4(1:4)
	DEC4	1
	DEC5	50
	J5P	4B
	OUT	0,4(TERM)
	LD4	24,4
	J5N	2B
	HLT
* INITIAL CONTENTS OF TABLES AND BUFFERS
	ORIG	PRIME+1
	CON	2
	ORIG	BUF0-5
TITLE	ALF	"FIRST"
	ALF	" FIVE"
	ALF	" HUND"
	ALF	"RED P"
	ALF	"RIMES"
	ORIG	BUF0+24
	CON	BUF1+10
	ORIG	BUF1+24
	CON	BUF0+10
	END	START
